group 'tomwamt'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.61'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

compileJava.enabled = false
jar.enabled = false

clean {
    delete 'out'
    delete 'testOut'
}

task assembleWeb(type: Copy, dependsOn: classes) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js")
            }
        })
    }
    from compileKotlin2Js.destinationDir
    include '**/*.js'
    includeEmptyDirs false
    into "${projectDir}/out/js"
}

task copyStaticWeb(type: Copy) {
    from 'web'
    exclude 'test.html'
    into 'out'
}

task copyResources(type: Copy, dependsOn: processResources) {
    from processResources.destinationDir
    into "${projectDir}/out"
}

assemble.dependsOn assembleWeb
assemble.dependsOn copyStaticWeb
assemble.dependsOn copyResources

task assembleTestWeb(type: Sync, dependsOn: testClasses) {
    configurations.testCompile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js")
            }
        })
    }

    from compileKotlin2Js.destinationDir
    from compileTestKotlin2Js.destinationDir
    include '**/*.js'
    includeEmptyDirs false
    into "${projectDir}/testOut/js"
}

task copyTestStaticWeb(type: Copy, dependsOn: assembleTestWeb) {
    from 'web'
    into 'testOut'
}

test.dependsOn copyTestStaticWeb

test {
    doLast {
        exec {
            commandLine "phantomjs", "testRunner/runner-list.js", "testOut/test.html"
        }
    }
}

/*[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "umd"
}*/
