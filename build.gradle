group 'tomwamt'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

clean {
    delete 'out'
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js")
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/out/js"

    dependsOn classes
}

task delKjsm(type: Delete) {
    delete fileTree('out') {
        include '**/*.kjsm'
    }

    dependsOn assembleWeb
}

task copyStaticWeb(type: Copy) {
    from 'web'
    into 'out'

    dependsOn delKjsm
}

assemble.dependsOn copyStaticWeb
